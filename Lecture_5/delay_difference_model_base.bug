model
{
##############################################################################################################################
##################### Priors ##################### Priors ##################### Priors##################### Priors
##############################################################################################################################



# priors for survey recruitment index, note we get get this estimate annually.
# This is a simple uniform which basically allows for anything with our R values.
for(t in 1:NY)
{
	R[t] ~ dlnorm(r.a, r.b)
}
	
# priors for natural mortality of both recruits and fully recruited, again we estimate these parameters annually.
# Priors of m.a =-2 and m.b = 2 results in a sampling distribution with a median of around 0.14, and m is generally < 1 though
# extreme values can happen.
M ~ dbeta(M.a, M.b)
m <- -log(1-M) # Convert from proportion to instantaneous rate



# Survey catchability prior, q priors should be between 0.2 and 0.5 (based on US dredge studies), q.a = 20, q.b = 40 gets us this result.
q ~ dbeta(q.a, q.b)


# All the variance related Priors start here.
# prior for process noise, uniform between 0 and 5.
sigma ~ dunif(sigma.a, sigma.b)
isigma2 <- pow(sigma, -2)



# Prior for the precision of the survey biomass index variance (that's a mouthful eh!)
I.precision ~ dgamma(I.precision.a,I.precision.b)
# Variance of the survey biomass index variance!
I.vsam <- 1/I.precision

# Prior for the precision of the survey recruit biomass index variance...
IR.precision ~ dgamma(IR.precision.a,IR.precision.b)
# And it's variance
IR.vsam <- 1/IR.precision

##############  END OF PRIORS ################################  END OF PRIORS ################################  END OF PRIORS ##################


##############################################################################################################################
##################### MODEL ##################### MODEL ##################### MODEL ##################### MODEL
##############################################################################################################################

#############Process Model#############Process Model#############Process Model#############Process Model
# Give Bmed a prior
Bmed[1] ~ dlnorm(bs.a, bs.b)
# Based on the initial value for Bmed, this gets an initial value for P
B[1] ~ dlnorm(log(Bmed[1]), isigma2)

# Here is our process/state equation from year 2 to the final year (likelihood) for the biomass
for(t in 2:NY)
{	
  # Our process model fit, natural mortality and catch remove individuals, growth terms increase size of individuals
  # and recruit term bring in the new recruits to the population.
	Bmed[t] <- log(max(exp(-m) * G[t-1] * (B[t-1] - C[t]) + exp(-m)*GR[t-1] * R[t-1], 0.001))
	# Now incorporate the process noise for our estimates of P
	B[t] ~ dlnorm(Bmed[t], isigma2)
}

############# Observation Models############# Observation Models############# Observation Models############# 

# observation models for fully-recruited and recruit biomass indicies and the catch rate index.
for(t in 1:NY)
{
	# Assumed relationship between observed survey biomass index and "true" biomass
	Imed[t] <- log(q *  B[t])
  # the observation error for the survey biomass index
	I[t] ~ dlnorm(Imed[t], I.precision)
	
	# Assumed relationship between observed survey recruit biomass index and "true" recruit biomass
	IRmed[t] <- log(q * R[t])
  # the observation error for the survey recruit biomass index
	IR[t] ~ dlnorm(IRmed[t], IR.precision)	
}

	
# Various derived quantities we're interested in.
for(t in 1:NY)
{


	# Modelled fully recruited survey biomass estimate
	Ipred[t] <- q * B[t]
	# Modelled recruit survey biomass estimate
	IRpred[t] <- q * R[t]
  # Exploitation rate
	mu[t] <- C[t]/(B[t]+C[t])
	# Instantaneous fishing mortality
	Fmort[t] <- -log(max(1 - mu[t], 0.0001))	
}


# Diagnostics
for(t in 1:NY)
{
  # Standard residuals for fully recruited and recruit biomass along with the Process term
	Iresid[t] <- log(I[t]) - Imed[t]
	IRresid[t] <- log(IR[t]) - IRmed[t]
	Presid[t] <- log(B[t]) - Bmed[t]


  # Same residuals but standardized (remember these are precisions mostly so multiply by sqrt(precision) = divide by standard deviation)
	sPresid[t] <- Presid[t] / sigma
	sIresid[t] <- Iresid[t] * pow(I.precision,0.5)
	sIRresid[t] <- IRresid[t] * pow(IR.precision,0.5)

}
}